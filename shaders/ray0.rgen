#version 460 core
#extension GL_EXT_ray_query : enable
#extension GL_EXT_buffer_reference : enable
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : enable
#extension GL_EXT_ray_tracing : enable

#include "/lib/rt/payload.glsl"
#include "/lib/rt/sampling.glsl"
#include "/lib/rand.glsl"
#include "/lib/constants.glsl"
#include "/lib/settings.glsl"

const vec3 SKY_COLOR = vec3(0.0112, 0.1273, 1.0) * 300.0;
const vec3 SUN_COLOR = vec3(1.0) * 1000.0;
const vec3 WATER_ABSORPTION = (1.0 - vec3(0.28, 0.44, 0.86)) * 0.05;

layout(std140, binding = 0) uniform CameraInfo {
    vec3 corners[4];
    mat4 viewInverse;
    vec3 sunAngle;
    uint frameId;
    uint flags;
} cam;

layout(binding = 1) uniform accelerationStructureEXT acc;

layout(binding = 2) buffer BlasDataAddresses { 
    uint64_t address[]; 
} quadBlobs;

layout(binding = 3) writeonly uniform image2D outImage;
layout(binding = 4) uniform sampler2D blockTex;

layout(location = 6) rayPayloadEXT Payload payload;

float fresnel(float cosTheta, float F0) {
    return F0 + (1.0 - F0) * pow(1.0 - cosTheta, 5.0);
}

void main() {
    initRNG(uvec2(gl_LaunchIDEXT.xy), cam.frameId);
    vec2  px        = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
    vec2  p         = px / vec2(gl_LaunchSizeEXT.xy);
    vec3  origin    = cam.viewInverse[3].xyz;
    vec3  target    = mix(mix(cam.corners[0], cam.corners[2], p.y), mix(cam.corners[1], cam.corners[3], p.y), p.x);
    vec3  direction = (cam.viewInverse * vec4(normalize(target.xyz), 0.0)).xyz;
    uint  rayFlags  = gl_RayFlagsCullBackFacingTrianglesEXT;
    float tMin      = 0.0001;
    float tMax      = 4096.0;

    bool translucent = (cam.flags & 3u) == 1u;
    vec3 absorption = translucent ? WATER_ABSORPTION : vec3(0.0);

    vec3 throughput = vec3(1.0);
    vec3 radiance = vec3(0.0);
    for (int i = 0; i < RAY_BOUNCES; i++) {
        traceRayEXT(
            acc, // acceleration structure
            rayFlags, // rayFlags
            0xFF, // cullMask
            0, // sbtRecordOffset // <- see comment [1] below
            0, // sbtRecordStride // <- see comment [1] below
            0, // missIndex
            origin, //origin       // ray origin
            tMin, // ray min range
            direction, // ray direction
            tMax, // ray max range
            6// payload (location = 6)
        );
        Payload hitPayload = payload;

        float t = hitPayload.hitData.w;

        if (translucent) {
            throughput *= exp(-t * absorption);
        }

        if (t < 0.0) {
            radiance += throughput * SKY_COLOR;
            break;
        }

        radiance += throughput * hitPayload.color.rgb * hitPayload.emission;
        origin = hitPayload.hitData.xyz;

        vec3 sunDir = normalize(vec3(1, 3, 2));
        sunDir = mix(cosineWeightedHemisphereSample(sunDir), sunDir, 0.95);
        if (hitPayload.color.a == 1.0 && dot(hitPayload.geometryNormal, sunDir) > 0.0) {
            float cosTheta = dot(hitPayload.normal, sunDir);
            traceRayEXT(
                acc, // acceleration structure
                rayFlags | gl_RayFlagsOpaqueEXT, // rayFlags
                0xFF, // cullMask
                0, // sbtRecordOffset // <- see comment [1] below
                0, // sbtRecordStride // <- see comment [1] below
                0, // missIndex
                origin,       // ray origin
                tMin, // ray min range
                sunDir, // ray direction
                tMax, // ray max range
                6// payload (location = 6)
            );
            if (payload.hitData.w < 0.0) {
                radiance += hitPayload.color.rgb * throughput * cosTheta * SUN_COLOR / PI;
            }
        }

        if (hitPayload.color.a < 1.0) {
            vec3 normal = hitPayload.normal;
            if (dot(normal, direction) > 0.0) {
                normal = -normal;
            }
            float fres = fresnel(dot(-direction, normal), 0.02);

            if (randFloat() < fres) {
                direction = reflect(direction, normal);
            } else {
                vec3 newDirection = refract(direction, normal, translucent ? 1.3333 : 1.0 / 1.3333);
                if (dot(newDirection, newDirection) < 0.01) {
                    throughput /= fres;
                    newDirection = reflect(direction, normal);
                    translucent = false;
                } else {
                    throughput /= 1.0 - fres;
                }
                direction = newDirection;
                if (translucent) {
                    absorption = vec3(0.0);
                } else {
                    absorption = WATER_ABSORPTION;
                    translucent = true;
                }
            }
        } else {
            direction = cosineWeightedHemisphereSample(hitPayload.normal);
            if (dot(direction, hitPayload.geometryNormal) <= 0.0) {
                direction = -direction;
            }
            throughput *= hitPayload.color.rgb;
        }
    }

    imageStore(outImage, ivec2(gl_LaunchIDEXT), vec4(radiance, 1.0));
}
